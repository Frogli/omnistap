If isclear(iirList.$calculation)
	Quit method iirList.$width
End If

Calculate llList as _ilList

Calculate lcCalculation as con("lcText.$assign(",iirList.$calculation,")")
Calculate lcCalculation as replaceall(lcCalculation,"_ilList","llList")

Test for valid calculation {eval(lcCalculation)}
If flag false
	Quit method iirList.$width
End If

Calculate $modes.$fixedcursor as kTrue

Calculate lcSortationCalculation as con("llList.$sort(len(",replaceall(iirList.$calculation,"_ilList","llList"),"),kTrue)")
Do eval(lcSortationCalculation)

For llList.$line from 1 to llList.$linecount step 1
	Do eval(lcCalculation)
	Calculate lnLineWidth as loFont.$getWidthForText(lcText,iirList.$font,iirList.$fontsize,iirList.$fontstyle)
	
	If lnLineWidth>lnListWidth
		Calculate lnListWidth as lnLineWidth
		Calculate lnIterationsWithoutResettingWidth as 0
	Else
		Calculate lnIterationsWithoutResettingWidth as lnIterationsWithoutResettingWidth+1
	End If
	
	If lnIterationsWithoutResettingWidth=20
		Break to end of loop
	End If
End For

Calculate lnListWidth as lnListWidth+30

Calculate $modes.$fixedcursor as kFalse

Quit method lnListWidth