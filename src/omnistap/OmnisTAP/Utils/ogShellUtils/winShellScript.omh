Calculate lnCT as #CT
Calculate lcScriptFilePath as con(pcFolder,"suran_script",lnCT,".bat")
Calculate lcVbsWrapperScriptPath2 as con(pcFolder,"suran_script_wrapper2",lnCT,".vbs")
Calculate lcVbsWrapperScriptPath as con(pcFolder,"suran_script_wrapper",lnCT,".vbs")
Calculate lcOutputFilePath as con(pcFolder,"suran_script",lnCT,".out")
Calculate lcReturnCodeFilePath as con(pcFolder,"suran_script",lnCT,".code")

# Write the command to a file
Begin text block
Text:@echo off(Carriage return,Linefeed)
Text:[pcCommand](Carriage return,Linefeed)

If pbBackground=kFalse
	Text:echo %ERRORLEVEL% > "[lcReturnCodeFilePath]"(Carriage return,Linefeed)
End If

End text block

Get text block lcScript

Do loFile.$createfile(lcScriptFilePath)
Do loFile.$writecharacter(kUniTypeAnsiLatin1,lcScript)
Do loFile.$closefile()

# Create a wrapper.vbs file to run the command and dump output to a file
Begin text block
Text:dim lcScriptPath, lcOutputPath(Carriage return,Linefeed)
Text:dim loShell(Carriage return,Linefeed)
Text:(Carriage return,Linefeed)
Text:Set loShell = CreateObject("WScript.Shell")(Carriage return,Linefeed)
Text:lcScriptPath = chr(34) & "[lcScriptFilePath]" & chr(34)(Carriage return,Linefeed)
Text:lcOutputPath = chr(34) & "[lcOutputFilePath]" & chr(34)(Carriage return,Linefeed)
Text:(Carriage return,Linefeed)
Text:loShell.Run lcScriptPath 

If pbBackground=kFalse
	Text: & " > " & lcOutputPath & " 2>>&1"
End If

Text:, 0, TRUE(Carriage return,Linefeed)
Text:(Carriage return,Linefeed)
Text:Set loShell = Nothing(Carriage return,Linefeed)
End text block

Get text block lcVbsWrapperScript2

Do loFile.$createfile(lcVbsWrapperScriptPath2)
Do loFile.$writecharacter(kUniTypeAnsiLatin1,lcVbsWrapperScript2)
Do loFile.$closefile()

If pbAdminPriviledges
	Calculate lcVerb as '"runas"'
Else
	Calculate lcVerb as '""'
End If

Begin text block
Text:dim lcScriptPath(Carriage return,Linefeed)
Text:dim loShell(Carriage return,Linefeed)
Text:(Carriage return,Linefeed)
Text:Set loShell = CreateObject("Shell.Application")(Carriage return,Linefeed)
Text:lcScriptPath = chr(34) & "[lcVbsWrapperScriptPath2]" & chr(34)(Carriage return,Linefeed)
Text:lcFlagFilePath = "[lcReturnCodeFilePath]"(Carriage return,Linefeed)
Text:(Carriage return,Linefeed)
Text:loShell.ShellExecute "wscript.exe", lcScriptPath, "", [lcVerb], 0(Carriage return,Linefeed)
Text:Set loShell = Nothing(Carriage return,Linefeed)

If pbBackground=kFalse
	Text:(Carriage return,Linefeed)
	Text:set loFSO = CreateObject("Scripting.FileSystemObject")(Carriage return,Linefeed)
	Text:Do(Carriage return,Linefeed)
	Text:   lbFlag = loFSO.FileExists(lcFlagFilePath)(Carriage return,Linefeed)
	Text:   Wscript.Sleep 100(Carriage return,Linefeed)
	Text:Loop Until lbFlag = True(Carriage return,Linefeed)
	Text:(Carriage return,Linefeed)
	Text:Set loFSO = Nothing(Carriage return,Linefeed)
End If

End text block

Get text block lcVbsWrapperScript

Do loFile.$createfile(lcVbsWrapperScriptPath)
Do loFile.$writecharacter(kUniTypeAnsiLatin1,lcVbsWrapperScript)
Do loFile.$closefile()

Calculate lcScriptToExecute as con("wscript.exe ",lcVbsWrapperScriptPath)

Launch program [lcScriptToExecute] Returns lcReturn

If pbBackground
	Quit method
End If

Do loFile.$openfile(lcOutputFilePath,kTrue)
Do loFile.$readcharacter(kUniTypeAuto,lcOutput)
Do loFile.$closefile()

If FileOps.$doesfileexist(lcReturnCodeFilePath)
	Do loFile.$openfile(lcReturnCodeFilePath,kTrue)
	Do loFile.$readcharacter(kUniTypeAuto,lnError)
	Do loFile.$closefile()
Else
	Calculate lnError as 1
End If

Calculate lcOutput as trim(lcOutput,kFalse,kTrue,kLf)
Calculate lcOutput as trim(lcOutput,kFalse,kTrue,kCr)
Calculate pfrResult as lcOutput

Do FileOps.$deletefile(lcScriptFilePath)
Do FileOps.$deletefile(lcVbsWrapperScriptPath2)
Do FileOps.$deletefile(lcVbsWrapperScriptPath)
Do FileOps.$deletefile(lcOutputFilePath)
Do FileOps.$deletefile(lcReturnCodeFilePath)

Quit method lnError