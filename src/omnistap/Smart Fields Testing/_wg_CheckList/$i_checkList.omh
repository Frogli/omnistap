Do $windows.$add("wtCheckListTest") Returns lirTestWindowClass
Do ioTAP.$isnotclear(lirTestWindowClass,"Test window class added")

Do lirTestWindowClass.$ivardefs.$add("ilCheckList",kList) Returns lirTestWindowVariableDef
Do ioTAP.$isnotclerar(lirTestWindowVariableDef,"Test ivar added")

Do lirTestWindowClass.$objs.$add(kSubwindow) Returns lirTestWindowSubwindow
Do ioTAP.$isnotclerar(lirTestWindowSubwindow,"Test subwindow field added")

Calculate lirTestWindowSubwindow.$dataname as "ilCheckList"
Calculate lirTestWindowSubwindow.$parameters as '"ilCheckList.Description"'
Calculate lirTestWindowSubwindow.$classname as "wg_CheckList"

Do lirTestWindowClass.$open() Returns lirTestWindowInstance
Do ioTAP.$isnotclear(lirTestWindowInstance,"Test window opened")

Do lirTestWindowInstance.$objs.$first() Returns lirFieldInst
Do ioTAP.$isnotclear(lirFieldInst,"Subwindow exists on window instance")
Set reference lirSubinst to lirFieldInst.$subinst
Do ioTAP.$isnotclear(lirSubinst,"Subwindow instance exists on window instance")

Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$linecount,0,"ilCheckList.$linecount is 0")
Do ioTAP.$is_number(lirSubinst._ilList.$linecount,0,"ilList.$linecount is 0")
Do ioTAP.$is_char(lirFieldInst.$objs.clList.$calculation,"_ilList.Description","Check list has the right definition")
Do ioTAP.$is_boolean(lirSubinst.ioTypeToSearch.ibReselectListLinesOnChange,kFalse,"We don't reselect list lines when changing from a type to search on check lists")

Do llCheckList.$cols.$add("Description",kCharacter,kSimplechar,10000000)
Do llCheckList.$add("Apple")
Do llCheckList.$add("Orange")
Do llCheckList.$add("Banana")

Calculate lirTestWindowInstance.ilCheckList as llCheckList
Do lirFieldInst.$redraw()

Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$linecount,3,"ilCheckList.$linecount is 3")
Do ioTAP.$is_number(lirSubinst._ilList.$linecount,3,"ilList.$linecount is 3")
Do ioTAP.$is_number(lirFieldInst.$objs.ppList.$objs.clList.$contents().$linecount,3,"Check list has the right contents")
Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$line,0,"ilCheckList.$line is 0")
Do ioTAP.$is_number(lirSubinst._ilList.$line,0,"ilList.$line is 0")
Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$totc($ref.0.$selected),0,"ilCheckList has no selected lines")
Do ioTAP.$is_number(lirSubinst._ilList.$totc($ref.0.$selected),0,"ilList has no selected lines")

Calculate lirTestWindowInstance.ilCheckList.2.$selected as kTrue
Do lirFieldInst.$redraw()

Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$totc(lirTestWindowInstance.ilCheckList.0.$selected),1,"ilCheckList has one selected line")
Do ioTAP.$is_number(lirSubinst._ilList.$totc(lirSubinst._ilList.0.$selected),1,"ilList has one selected line")

Do lirFieldInst.$objs.pbAll.$click()
Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$totc(lirTestWindowInstance.ilCheckList.0.$selected),lirTestWindowInstance.ilCheckList.$linecount,"ilCheckList has all lines selected")
Do ioTAP.$is_number(lirSubinst._ilList.$totc(lirSubinst._ilList.0.$selected),lirSubinst._ilList.$linecount,"ilList has all lines selected")
Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$line,0,"ilCheckList.$line is 0")
Do ioTAP.$is_number(lirSubinst._ilList.$line,0,"ilList.$line is 0")

Do lirFieldInst.$objs.pbNone.$click()
Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$totc(lirTestWindowInstance.ilCheckList.0.$selected),0,"ilCheckList has no lines selected")
Do ioTAP.$is_number(lirSubinst._ilList.$totc(lirSubinst._ilList.0.$selected),0,"ilList has no lines selected")
Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$line,0,"ilCheckList.$line is 0")
Do ioTAP.$is_number(lirSubinst._ilList.$line,0,"ilList.$line is 0")

Calculate lirSubinst._ilList.$line as 1
Do lirFieldInst.$objs.clList.$click()
Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$line,1,"ilCheckList.$line is 1")

Calculate lirSubinst._ilList.$line as 2
Do lirFieldInst.$objs.clList.$click(kTrue)
Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$line,2,"ilCheckList.$line is 2")

Do ioTAP.$is_boolean(lirTestWindowInstance.ilCheckList.1.$selected,kTrue,"ilCheckList.1 is selected")
Do ioTAP.$is_boolean(lirTestWindowInstance.ilCheckList.2.$selected,kTrue,"ilCheckList.2 is selected")
Do ioTAP.$is_boolean(lirSubinst._ilList.1.$selected,kTrue,"ilList.1 is selected")
Do ioTAP.$is_boolean(lirSubinst._ilList.2.$selected,kTrue,"ilList.2 is selected")

Calculate lirSubinst._ilList.$line as 1
Do lirFieldInst.$objs.clList.$click(kTrue)
Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$line,1,"ilCheckList.$line is 1")

Do ioTAP.$is_number(lirTestWindowInstance.ilCheckList.$totc(lirTestWindowInstance.ilCheckList.0.$selected),0,"ilCheckList has no lines selected")
Do ioTAP.$is_number(lirSubinst._ilList.$totc(lirSubinst._ilList.0.$selected),0,"ilList has no lines selected")

Calculate lirSubinst._ilList.$line as 3
Do lirFieldInst.$listDoubleClicked()
Do ioTAP.$is_boolean(lirTestWindowInstance.ilCheckList.3.$selected,kTrue,"ilCheckList.3 is selected")
Do ioTAP.$is_boolean(lirSubinst._ilList.3.$selected,kTrue,"ilList.3 is selected")

Do lirFieldInst.$listDoubleClicked()
Do ioTAP.$is_boolean(lirTestWindowInstance.ilCheckList.3.$selected,kFalse,"ilCheckList.3 is not selected")
Do ioTAP.$is_boolean(lirSubinst._ilList.3.$selected,kFalse,"ilList.3 is not selected")

Calculate lirFieldInst.$width as 50
Calculate lirFieldInst.$height as 50
Do lirFieldInst.$setDimensions()

Do ioTAP.$is_boolean(lirFieldInst.$objs.clList.$horzscroll,kTrue,"Field has horizontal scrollbars when too small")
Do ioTAP.$is_boolean(lirFieldInst.$objs.clList.$vertscroll,kTrue,"Field has vertical scrollbars when too small")

Calculate lirFieldInst.$width as 500
Do lirFieldInst.$setDimensions()
Do ioTAP.$is_boolean(lirFieldInst.$objs.clList.$horzscroll,kFalse,"Field doesn't have horizontal scrollbars when wide enough")

Do lirTestWindowInstance.$close()
Do $classes.$remove(lirTestWindowClass)

Do ioTAP.$isclear(lirTestWindowClass,"Class removed")