Calculate lcScriptFileName as con("suran_script",#CT,".sh")
Calculate lcScriptFilePath as con(pcFolder,lcScriptFileName)
If FileOps.$doesfileexist(lcScriptFilePath)
	Do FileOps.$deletefile(lcScriptFilePath) Returns #F
End If

Begin text block
Text:[pcCommand]

If pbBackground=kFalse
	Text:(Linefeed)
	Text:liOmnisShellResult=$?(Linefeed)
	Text:echo ${liOmnisShellResult}(Linefeed)
End If

End text block

Get text block lcCDMRunBody

Do loFile.$createfile(lcScriptFilePath)
Do loFile.$writecharacter(kUniTypeAnsiLatin1,lcCDMRunBody)
Do loFile.$closefile()

Do FileOps.$setunixpermissions(lcScriptFilePath,"-rwxrwxrwx")

Begin text block
Text:do shell script "/bin/bash [lcScriptFilePath] [pick(pbBackground,"2>&1;","> /dev/null 2>&1 & echo $!")]"
If pbAdminPriviledges
	If $ctask.tbTestMode&tvDevVersion=kFalse
		Text: user name "[pcUserName]" password "[pcPassword]" with administrator privileges
	Else
		Text: with administrator privileges
	End If
End If
End text block

Get text block lcScript

Do $root.$runapplescript(lcScript,pfrResult) Returns lnError

If lnError=-1753
	Do $cinst.$wait(60)
	
	Do $root.$runapplescript(lcScript,pfrResult) Returns lnError
End If

If pbBackground=kTrue
	Quit method lnError
End If

Do FileOps.$deletefile(lcScriptFilePath)

Calculate lcScriptFilePath as con(pcFolder,"cdm_run.sh")
If FileOps.$doesfileexist(lcScriptFilePath)
	Do FileOps.$deletefile(lcScriptFilePath)
End If

If lnError<>0
	Quit method lnError
End If

Calculate pfrResult as trim(pfrResult,kTrue,kTrue,'"')
Calculate lnLastLinePos as rpos(kCr,pfrResult)
Calculate lnError as mid(pfrResult,lnLastLinePos+1)
Calculate pfrResult as mid(pfrResult,1,lnLastLinePos-1)

Quit method lnError